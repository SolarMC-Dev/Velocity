
java {
    // To change the JDK version, velocity-api/build.gradle also must be updated
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}
tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}
tasks.withType(Test) {
    jvmArgs += '--enable-preview'
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv("NEXUS_USERNAME")
                password System.getenv("NEXUS_PASSWORD")
            }

            name = 'velocity-nexus'
            def base = 'https://nexus.velocitypowered.com/repository/velocity-artifacts'
            def releasesRepoUrl = "$base-release/"
            def snapshotsRepoUrl = "$base-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

testlogger {
    showFullStackTraces true
    showStandardStreams true
}
