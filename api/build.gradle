plugins {
    id 'java-library'
    id 'maven-publish'
    //id 'checkstyle'
    id "com.adarshr.test-logger"
}

//apply from: '../gradle/checkstyle.gradle'
apply from: '../gradle/common.gradle'
apply plugin: 'org.cadixdev.licenser'

license {
    header = project.file('HEADER.txt')
}

sourceSets {
    ap {
        compileClasspath += main.compileClasspath + main.output
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'
    api "com.google.guava:guava:${guavaVersion}"

    // DEPRECATED: Will be removed in Velocity 2.0.0
    api "net.kyori:text-api:${textVersion}"
    api "net.kyori:text-serializer-gson:${textVersion}"
    api "net.kyori:text-serializer-legacy:${textVersion}"
    api "net.kyori:text-serializer-plain:${textVersion}"

    api(platform("net.kyori:adventure-bom:${adventureVersion}"))
    api("net.kyori:adventure-api")
    api("net.kyori:adventure-text-serializer-gson")
    api("net.kyori:adventure-text-serializer-legacy")
    api("net.kyori:adventure-text-serializer-plain")
    api("gg.solarmc:adventure-text-serializer-legacy-text3:1.0.1") {
        transitive = false
    }

    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api 'com.google.inject:guice:4.2.3'
    api "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    api 'com.velocitypowered:velocity-brigadier:1.0.0-SNAPSHOT'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    // SolarMC
    api "gg.solarmc.loader:loader-core:${dataLoaderVersion}"
    api "space.arim.omnibus:omnibus:1.1.0-M2"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    from sourceSets.ap.output
}

jar {
    from sourceSets.ap.output
}

/*
shadowJar {
    from sourceSets.ap.output
}
*/

artifacts {
    archives javadocJar
//    archives shadowJar
    archives sourcesJar
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'http://www.slf4j.org/apidocs/',
            'https://google.github.io/guava/releases/25.1-jre/api/docs/',
            'https://google.github.io/guice/api-docs/4.2/javadoc/',
            'https://docs.oracle.com/en/java/javase/16/docs/api/',
            'https://jd.adventure.kyori.net/api/4.7.0/'
    )

    options.addStringOption('-release', '16')

    // Disable the crazy super-strict doclint tool in Java 8
    options.addStringOption('Xdoclint:none', '-quiet')

    // Remove 'undefined' from seach paths when generating javadoc for a non-modular project (JDK-8215291)
    if (JavaVersion.current() >= JavaVersion.VERSION_1_9 && JavaVersion.current() < JavaVersion.VERSION_12) {
        options.addBooleanOption('-no-module-directories', true)
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
